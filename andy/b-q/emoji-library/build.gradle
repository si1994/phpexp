apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'
// =================================================================================================
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

group = 'br.com.edsilfer.android'
version = '1.0.9'
// =================================================================================================
android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.6"
        multiDexEnabled true
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}
// =================================================================================================
// BINTRAY UPLOAD TASKS
ext {
    bintrayRepo = properties.getProperty('repo.main')
    bintrayName = properties.getProperty('repo.name')
    publishedGroupId = group
    libraryName = properties.getProperty('repo.label')
    artifact = properties.getProperty('repo.name')
    libraryDescription = properties.getProperty('repo.description')
    siteUrl = properties.getProperty('repo.url')
    gitUrl = properties.getProperty('repo.vcs')
    libraryVersion = version
    developerId = properties.getProperty('dev.nickname')
    developerName = properties.getProperty('dev.name')
    developerEmail = properties.getProperty('repo.email')
    licenseName = properties.getProperty('repo.license.name')
    licenseUrl = properties.getProperty('repo.license.url')
    allLicenses = [properties.getProperty('repo.license.label')]
    apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
    apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'
}


bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')
    configurations = ['published', 'archives']
    pkg {
        repo = properties.getProperty('repo.main')
        name = properties.getProperty('repo.name')
        desc = properties.getProperty('repo.description')
        websiteUrl = properties.getProperty('repo.url')
        issueTrackerUrl = properties.getProperty('repo.issues')
        vcsUrl = properties.getProperty('repo.vcs')
        labels = ['android', 'searchable', 'interface', 'layout']
        publicDownloadNumbers = true
    }
}
// =================================================================================================
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.ogaclejapan.smarttablayout:library:1.6.0@aar'
    implementation 'com.github.satyan:sugar:1.5'
    implementation 'com.google.code.gson:gson:2.7'
    implementation 'com.github.bumptech.glide:glide:4.7.1'
}
